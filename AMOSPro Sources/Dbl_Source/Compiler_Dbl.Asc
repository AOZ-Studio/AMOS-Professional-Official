
main dialog program of the amospro compiler shell

main panel

LAbel    0;
SIze     SW,SH;
BAse     SWidth SX -2/,SHeight SY -2/;
SetVar   0,SX 640- 16/ 8*;
UNpack   32,0,60;
LIne     424,0,61,SX56-;
BUtton   0,32,0,SX96-,16,0,0,0;[][SM;]
BUtton   15,SX 56-,0,56,16,0,0,1;[UNpack 0,0,76BP+;][]
BUtton   1,0,0,32,16,0,0,1;[UNpack 0,0,55BP+;][BR0;]
KY       27,0;
INk      0,0,0; GLine 0,16,SX,16;
BOx      0,17,1,SX,SY;
BOx      8 0VA+,28,1,8 216+ 0VA+,110;
UNpack   72 0VA+,40,57;
RotBut   2,8 8+ 0VA+,68,200;
BOx      232 0VA+,28,1,232 216+ 0VA+,110;
UNpack   200 0VA+,40,64;
UNpack   264 0VA+,40,58;
RotBut   3,232 8+ 0VA+,68,200;
BOx      456 0VA+,28,1,SX 0VA- 8-,110;
UNpack   512 0VA+,40,59;
RotBut   4,456 8+ 0VA+,68,160;
BUtton   5,24 0VA+,120,184,24,0,0,1;[LIne 0,0,33 3BP*+,184; UN 16,4BP+,53;PR 56,THBP+,99ME,3;][BR0;]
BUtton   6,SX 384- 0VA- 24-,120,384,24,0,0,1;[LIne 0,0,33 3BP*+,384;UN 120,4BP+,54;][BR0;]
EXit;
      
setup main panel

LAbel    1;
SIze     600,150;
BAse     SW SX-2/,SH SY-2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX 64-,20;
POutline 130MECX,8,130ME,0,7;
BUtton   0,0,0,SX64-,16,0,0,0;[][SM;]
BUtton   15,SX 64-,4,56,16,0,0,1;[UNpack 0,0,76BP+;][]
BUtton   3,32,26,SX32-,24,0,0,1;[LIne 0,0,33 3BP*+,SX 32-; PRint 131ME CX,8,131ME,3;][BR0;]
BUtton   4,32,54,SX32-,24,0,0,1;[LIne 0,0,33 3BP*+,SX 32-; PRint 132ME CX,8,132ME,3;][BR0;]
BUtton   5,32,82,SX32-,16,0,0,1;[LIne 0,0,68 3BP*+,SX 32-; PRint 133ME CX,4,133ME,3;][BR0;]
BUtton   6,32,SY40-,135METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,135METW16+; PRint 135MECX BP+,4,135ME,3;][BR0;]
BUtton   7,SX 136METW- 24- 2/,SY40-,136METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,136METW16+; PRint 136MECX BP+,4,136ME,3;][BR0;]
BUtton   8,SX 137METW- 48- ,SY40-,137METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,137METW16+; PRint 137MECX BP+,4,137ME,3;][BR0;]
BUtton   1,32,SY22-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4MECX BP+,4,4ME,3;][BR0;]
BUtton   2,SX96-,SY22-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 5MECX BP+,4,5ME,3;][BR0;]
EXit;

setup compiled prog one
      
LAbel 2;
SIze     560,140;
BAse     SW SX-2/,SH SY-2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX 64-,20;
POutline 140MECX16-,8,140ME' - 1'!,0,7;
BUtton   0,0,0,SX64-,16,0,0,0;[][SM;]
BUtton   15,SX 64-,4,56,16,0,0,1;[UNpack 0,0,76BP+;][]
DOption  3,16,32,SX16-,141,4VA;
DOption  4,16,44,SX16-,142,5VA;
DOption  5,16,56,SX16-,143,6VA;
DOption  6,16,68,SX16-,144,7VA;
BUtton   1,16,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4MECX BP+,4,4ME,3;][BR0;]
KY       27,0;
BUtton   2,SX80-,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3MECX BP+,4,3ME,3;][BR0;]
KY       13,0;
BUtton   8,SX 8METW- 16- 2/,SY20-,8METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,8METW16+; PRint 8MECX BP+,4,8ME,3;][BR0;]
EXit;

setup compiled prog two

LAbel 3;
SIze     560,140;
BAse     SW SX-2/,SH SY-2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX64-,20;
POutline 140MECX16-,8,140ME' - 2'!,0,7;
BUtton   0,0,0,SX 64-,16,0,0,0;[][SM;]
BUtton   15,SX 64-,4,56,16,0,0,1;[UNpack 0,0,76BP+;][]
DOption  3,16,32,SX16-,151,14VA;
DOption  4,16,44,SX16-,152,15VA;
BUtton   1,16,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4MECX BP+,4,4ME,3;][BR0;]
KY       27,0;
BUtton   2,SX80-,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3MECX BP+,4,3ME,3;][BR0;]
KY       13,0;
BUtton   8,SX 8METW- 16- 2/,SY20-,8METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,8METW16+; PRint 8MECX BP+,4,8ME,3;][BR0;]
EXit;

setup compiler shell one

LAbel 4;
SIze     560,140;
BAse     SW SX-2/,SH SY-2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX 64-,20;
POutline 160MECX16-,8,160ME' - 1'!,0,7;
BUtton   0,0,0,SX64-,16,0,0,0;[][SM;]
BUtton   15,SX 64-,4,56,16,0,0,1;[UNpack 0,0,76BP+;][]
DOption  3,16,32,SX16-,161,24VA;
DOption  4,16,44,SX16-,162,25VA;
DOption  5,16,56,SX16-,163,26VA;
DOption  6,16,68,SX16-,164,27VA;
BUtton   1,16,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4MECX BP+,4,4ME,3;][BR0;]
KY       27,0;
BUtton   2,SX80-,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3MECX BP+,4,3ME,3;][BR0;]
KY       13,0;
BUtton   8,SX8METW- 16- 2/,SY20-,8METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,8METW16+; PRint 8MECX BP+,4,8ME,3;][BR0;]
EXit;

setup compiler shell two

LAbel 5;
SIze     560,140;
BAse     SW SX-2/,SH SY-2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX 64-,20;
SetVar   0,SW SX- 2/;
SetVar   1,SH SY- 2/;
POutline 160MECX16-,8,160ME' - 2'!,0,7;
BUtton   0,0,0,SX64-,16,0,0,0;[][SM;]
BUtton   15,SX 64-,4,56,16,0,0,1;[UNpack 0,0,76BP+;][]
DOption  3,16,32,SX16-,171,34VA;
DSelect  7,16,44,SX16-;
DOption  4,16,56,SX16-,172,35VA;
DSelect  8,16,68,SX16-;
DOption  5,16,80,SX16-,173,36VA;
DOption  6,16,92,SX16-,174,37VA;
BUtton   1,16,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4MECX BP+,4,4ME,3;][BR0;]
KY       27,0;
BUtton   2,SX80-,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3MECX BP+,4,3ME,3;][BR0;]
KY       13,0;
BUtton   9,SX8METW- 16- 2/,SY20-,8METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,8METW16+; PRint 8MECX BP+,4,8ME,3;][BR0;]
EXit;

setup compiler system

LAbel 6;
SIze     560,140;
BAse     SW SX-2/,SH SY-2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX 64-,20;
POutline 180MECX,8,180ME,0,7;
BUtton   0,0,0,SX64-,16,0,0,0;[][SM;]
BUtton   15,SX 64-,4,56,16,0,0,1;[UNpack 0,0,76BP+;][]
BUtton   3,16,32,SX16-,16,0,0,1;[LIne 0,0,68 3BP*+,SX16-; PRint 181MECX BP+,4,181ME,3;][BR0;]
BUtton   4,16,50,SX16-,16,0,0,1;[LIne 0,0,68 3BP*+,SX16-; PRint 182MECX BP+,4,182ME,3;][BR0;]
BUtton   5,16,68,SX16-,16,0,0,1;[LIne 0,0,68 3BP*+,SX16-; PRint 183MECX BP+,4,183ME,3;][BR0;]
BUtton   6,16,86,SX16-,16,0,0,1;[LIne 0,0,68 3BP*+,SX16-; PRint 184MECX BP+,4,184ME,3;][BR0;]
BUtton   1,16,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4MECX BP+,4,4ME,3;][BR0;]
KY       27,0;
BUtton   2,SX80-,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3MECX BP+,4,3ME,3;][BR0;]
KY       13,0;
EXit;
      
list of strings 
      
LAbel 7;
SIze     496,140;
BAse     SW SX-2/,SH SY-2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX 64-,20;
POutline 0VACX,8,0VA,0,7;
BUtton   0,0,0,SX64-,16,0,0,0;[][SM;]
BUtton   15,SX64-,4,56,16,0,0,1;[UNpack 0,0,76BP+;][]
VLine    SX32-,24,30,116;
VSlider  3,SX24-,28,8,84,3VA,10,1VA,1;[ZChange 4,ZPos; SV 3,ZPos;]
BOx      8,24,21,SX32-,116;
AList    4,32,30,SX80-8/,10,2VA,3VA,3,2,3;[]
BUtton   1,16,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4MECX BP+,4,4ME,3;][BR0;]
KY       27,0;
BUtton   2,SX80-,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3MECX BP+,4,3ME,3;][BR0;]
KY       13,0;
EXit;
      
popup string edit
      
LAbel    8:
SIze     480,64;
BAse     SW SX-2/,SH SY-2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX 8-,20;
POutline 0VACX,8,0VA,0,7;
BUtton   0,0,0,SX,16,0,0,0;[][SM;]
LIne     8,24,12,472;
EDit     3,24,28,56,255,1VA,0,3;
BUtton   1,16,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4MECX BP+,4,4ME,3;][BR0;]
KY       27,0;
BUtton   2,SX80-,SY20-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3MECX BP+,4,3ME,3;][BR0;]
KY       13,0;
EXit;
      
about
      
LAbel    9:
SIze     360,100;
BAse     SW SX -2/,SH SY -2/; SA 1;
BOx      0,0,1,SX,SY;
BOx      16,8,1,SX 16-,72;
SVar     2,1ME 0VA!;
POutilne 2 VA CentreX,8 TH+,2VA,0,7;
SVar     2,10ME ' Francois LIONET'!;
PRint    2VA CentreX,24 TH+,2VA,7;
SVar     2,11ME ' Jean-Baptiste BOLCATO'!;
PRint    2VA CentreX,36 TH+,2VA,7;
SVar     2,'© 1993 Europress Software Ltd.';
PRint    2VA CentreX,48 TH+,2 VA,7;
PRint    1VA CentreX,78 TH+,1 VA,3;
EXit;
      
compiler vumeter 
      
LAbel    10:
SIze     480,120;
BAse     SW SX -2/,SH SY -2/;
BOx      0,0,1,SX,SY;
BOx      16,8,1,SX 16-,32;
BOx      16,40,1,SX 16-,SY 8-;
POutilne 13ME CentreX,8 TH+,13ME,0,7;
BOx      32,48,21,SX 32-,SY 40-;
PRint    32,SY 44- TH+,'0%',5;
PRint    SX 64-,SY 44- TH+,'100%',5;
EXit;
      
edit list of progs 
      
LAbel    11:
SIze     586,144;
BAse     SW SX -2/,SH SY -2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,256 64-,20;
POutline 20,8,15ME,0,7;
BUtton   15,192,4,56,16,0,0,1;[UNpack 0,0,76BP+;][]
BOx      280,8,21,580,120;
BUtton   0,0,0,256,32,0,0,0;[][SM;]
BUtton   1,16,SY24-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4ME CX BP+,4,4ME,3;][BR0;]
KY       27,0;
BUtton   2,256 64- 16-,SY24-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3ME CX BP+,4,3ME,3;][BR0;]
BUtton   3,16,40,256 32-,24,0,0,1;[LIne 0,0,33 3BP*+,256 32-; PRint 16ME CX BP+,8,16ME,3;][BR0;]
BUtton   4,16,72,256 32-,24,0,0,1;[LIne 0,0,33 3BP*+,256 32-; PRint 17ME CX BP+,8,17ME,3;][BR0;]
BUtton   5,256,SY24-,160,16,0,0,1;[LIne 0,0,68 3BP*+,160; PRint 18ME CX BP+,4,18ME,3;][BR0;]
BUtton   6,416,SY24-,160,16,0,0,1;[LIne 0,0,68 3BP*+,160; PRint 19ME CX BP+,4,19ME,3;][BR0;]
VLine    256,8,30,120;
VSlider  7,264,12,8,104,0,12,1VA,1;[ZChange 8,ZPos;]
AList    8,304,16,256 8/,100 8/,0VA,0,4,2,3;[]
EXit;
      
compile report 
      
LAbel    12:
SIze     0VA,1VA;
BAse     SW SX -2/,SH SY -2/;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX 8-,20;
POutline 3VA CentreX,8,3VA,0,7;
PRint    4VA CentreX,28,4VA,3;
PRint    5VA CentreX,44,5VA,3;
PRint    6VA CentreX,56,6VA,3;
BUtton   0,0,0,SX,20,0,0,0;[][SM;]
IF       2VA1=; [
  BUtton 1,SX 2/ 32-,SY24-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3ME CX BP+,4,3ME,3;][BR0;]
  KY     13,0;]
IF       2VA2=; [
  BUtton 1,16,SY24-,113METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,113METW16+; PRint 113ME CX BP+,4,113ME,3;][BR0;]
  KY     13,0;
  BUtton 2,SX 114METW-16-16-,SY24-,114METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,114METW16+; PRint 114ME CX BP+,4,114ME,3;][BR0;]
  KY     27,0;]
IF       2VA3=; [
  BUtton 1,16,SY24-,118METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,118METW16+; PRint 118ME CX BP+,4,118ME,3;][BR0;]
  KY     13,0;
  BUtton 2,SX 117METW-16-16-,SY24-,117METW16+,16,0,0,1;[LIne 0,0,68 3BP*+,117METW16+; PRint 117ME CX BP+,4,117ME,3;][BR0;]
  KY     27,0;]
EXit;
      
warning requester

LAbel    13;
SIze     1VA TW 32+, 48 0VA 24*+;
BAse     SW SX -2/,SH SY -2/; SA 1;
BOx      0,0,1,SX,SY;
BOx      8,4,1,SX 8-,20;
POutline 21ME CentreX,8,21ME,0,7;
PRint    1VA CentreX,20 TH+,1VA,3;
BUtton   0,0,0,SX,32,0,0,0;[][SM;]
IF       0VA 1=;[
BUtton   1,16,SY24-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 4ME CX BP+,4,4ME,3;][BR0;]
KY       27,0;
BUtton   2,SX80-,SY24-,64,16,0,0,1;[UNpack 0,0,10BP+; PRint 3ME CX BP+,4,3ME,3;][BR0;]
KY       13,0;  ]
EXit;

information
      
LAbel    14:
SIze     0VATW 64+,40;
BAse     SW SX -2/,SH SY -2/; SA 1;
BOx      0,0,1,SX,SY;
PRint    0VA CentreX,16,0VA,7;
EXit;


information and vumeter
      
LAbel    15:
SIze     0VA TW 32+,72;
BAse     SW SX -2/,SH SY -2/; SA 1;
BOx      0,0,1,SX,SY;
PRint    0VA CentreX,12,0VA,7;
BOx      32,28,21,SX 32-,50;
PRint    32,SY 24- TH+,'0%',5;
PRint    SX 64-,SY 24- TH+,'100%',5;
EXit;


user instructions
      
UI       DO,6;
         drawoption zn x y w me bp
[
LIne     P2,P3,65,P4 68-;
PRint    P2 16+,P3 1+,P5ME,3;
BUtton   P1,P4 64-,P3,64,10,P6,0,1;
         [LIne 0,0,15 3BP*+,64; PRint 6BP+MECX BP+,1,6BP+ME,3;][]
]
      
UI       DS,4;
         drawselect zn x y w
[
LIne     P2,P3,65,P4 28-;
BUtton   P1,P4 24-,P3,24,10,0,0,1;
         [UNpack 0,0,74BP+;][BR0;]
]
      
UI       RB,4;
         rotating button zn x y w
[
SetZon P4;
BUtton P1,P2,P3,P4,24,0,0,1;
       [LIne 0,0,39 3BP*+,SX; UN 48,4BP+,45 ZN50+1-VA+;
       PRint 88,THBP+,91 ZN50+1-VA+ ME,3;]
       [BR0; SV ZN50+1-,ZN10* 40+ ZN50+1-VA+ VA;]
]
